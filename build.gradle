import java.text.SimpleDateFormat

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'idea'


sourceCompatibility = targetCompatibility = '1.8'

if(JavaVersion.current() != JavaVersion.VERSION_1_8)
    throw new GradleException(
            "This project requires Java 8, but it's running on "
                    +JavaVersion.current()
    )

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://jitpack.io" }
}

group 'de.griefed.serverPackCreator'

sourceSets {
    main {
        java {
            java.srcDirs = ['src/main/java']
        }
    }
}

configurations {
    embed
    compile.extendsFrom(embed)
}

dependencies {
    embed group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.14.0'
    embed group: 'com.typesafe', name: 'config', version: '1.4.1'
    embed group: 'com.github.FabricMC', name:'fabric-installer', version:'master-SNAPSHOT'
}

tasks.withType(Javadoc) {
    options.addStringOption('encoding', 'UTF-8')
}

javadoc {
    options.memberLevel = JavadocMemberLevel.PRIVATE
    classpath = sourceSets.main.runtimeClasspath
}
java {
    withSourcesJar()
    withJavadocJar()
}

jar {
    configurations.embed.each { dep ->
        from(project.zipTree(dep)) {
            exclude (
                    'META-INF/org',
                    'META-INF/org/**',
                    'META-INF/versions',
                    'META-INF/versions/**',
                    'META-INF/DEPENDENCIES',
                    'META-INF/LICENSE',
                    'META-INF/NOTICE',
                    'Log4j-**',
                    'icon.png',
                    'profile_icon.png'
            )
        }
    }
    manifest {
        attributes(
                'Main-Class'     : 'de.griefed.serverPackCreator.Main',
                'Description'    : 'Create serverpacks from Minecraft modpacks.',
                'Built-By'       : System.properties['user.name'],
                'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                'Created-By'     : "Gradle ${gradle.gradleVersion}",
                'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}",
                'Dependencies'   : configurations.runtime.files.collect {"$it.name" }.join(', ')
        )
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Griefed/ServerPackCreator")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        //noinspection GroovyAssignabilityCheck
        gpr(MavenPublication) {
            //noinspection GroovyAssignabilityCheck
            artifactId='serverpackcreator'
            //noinspection GroovyAssignabilityCheck
            from (components.java)
        }
    }
}